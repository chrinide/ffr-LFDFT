\subsection{Nonlocal pseudopotential}

Nonlocal pseudopotential contribution to total energy using HGH
pseudopotential can be written as.

\begin{multline}
E_{\mathrm{ps,NL}} = \Omega \sum_{i_{st}}^{N_{\mathrm{occ}}} f_{i_{st}}
\sum_{I_{a}}^{N_{\mathrm{atom}}}
\sum_{l=0}^{l_{\mathrm{max}}} \sum_{m=-l}^{l}
\sum_{i_p,j_p} h_{i_p,j_p}
\left[
\sum_{\alpha\beta\gamma}^{N_{\mathrm{points}}}
C^{i_{st}}_{\alpha\beta\gamma} \mathrm{B}_{i_{st}I_{a}lm}^{\mathrm{NL}}\left(
\mathbf{r}_{\alpha\beta\gamma}-\mathbf{R}_{I_{a}}\right)
\right]\\
\left[
\sum_{\alpha'\beta'\gamma'}^{N_{\mathrm{points}}}
C^{i_{st}}_{\alpha'\beta'\gamma'} \mathrm{B}_{i_{st}I_{a}lm}^{\mathrm{NL}}\left(
\mathbf{r}_{\alpha'\beta'\gamma'}-\mathbf{R}_{I_{a}}\right)
\right]
\end{multline}

This equation can be implemented as follows.
\begin{fortrancode}
E_ps_NL = 0.d0
DO ist = 1,Nstates_occ
  enl1 = 0.d0
  DO ia = 1,Natoms
    isp = atm2species(ia)
    DO l = 0,Ps(isp)%lmax
    DO m = -l,l
      DO iprj = 1,Ps(isp)%Nproj_l(l)
      DO jprj = 1,Ps(isp)%Nproj_l(l)
        ibeta = prj2beta(iprj,ia,l,m)
        jbeta = prj2beta(jprj,ia,l,m)
        hij = Ps(isp)%h(l,iprj,jprj)
        enl1 = enl1 + hij*betaNL_psi(ia,ist,ibeta)*betaNL_psi(ia,ist,jbeta)
      ENDDO ! jprj
      ENDDO ! iprj
    ENDDO ! m
    ENDDO ! l
  ENDDO 
  E_ps_NL = E_ps_NL + Focc(ist)*enl1
ENDDO 
\end{fortrancode}


Nonlocal HGH pseudopotential action can be written as follows.
\begin{equation}
\hat{V}_{\mathrm{ps,NL}} \psi_{i_{st}}(\mathbf{r}_{\alpha\beta\gamma}) =
\sum_{i_{st}}^{N_{\mathrm{occ}}} \sum_{I_{a}}^{N_{\mathrm{atom}}}
\sum_{l=0}\sum_{m=-l}^{+l} \sum_{i_p,j_p}
h_{i_p,j_p}
\mathrm{B}_{i_{st}I_{a}lm}^{\mathrm{NL}}\left(
\mathbf{r}_{\alpha\beta\gamma}-\mathbf{R}_{I_{a}}\right)
\left[
\sum_{\alpha'\beta'\gamma'}^{N_{\mathrm{points}}}
C^{i}_{\alpha'\beta'\gamma'} \mathrm{B}_{i_{st}I_{a}lm}^{\mathrm{NL}}\left(
\mathbf{r}_{\alpha'\beta'\gamma'}-\mathbf{R}_{I_{a}}\right)
\right]
\end{equation}

This equation is implemented as follows.
\begin{fortrancode}
IF( NbetaNL <= 0 ) THEN
  RETURN
ENDIF
Vpsi(:,:) = 0.d0
DO ist = 1,Nstates
  DO ia = 1,Natoms
    isp = atm2species(ia)
    DO l = 0,Ps(isp)%lmax
    DO m = -l,l
      DO iprj = 1,Ps(isp)%Nproj_l(l)
      DO jprj = 1,Ps(isp)%Nproj_l(l)
        ibeta = prj2beta(iprj,ia,l,m)
        jbeta = prj2beta(jprj,ia,l,m)
        hij = Ps(isp)%h(l,iprj,jprj)
        Vpsi(:,ist) = Vpsi(:,ist) + hij*betaNL(:,ibeta)*betaNL_psi(ia,ist,jbeta)
      ENDDO ! jprj
      ENDDO ! iprj
    ENDDO ! m
    ENDDO ! l
  ENDDO
ENDDO
\end{fortrancode}

The array {\tt betaNL} is defined initialized in subroutine {\tt init\_betaNL}:
\begin{fortrancode}
ALLOCATE( betaNL(Npoints,NbetaNL) )
! loop structure must be the same as in init_PsPot
ibeta = 0
DO ia = 1,Natoms
  isp = atm2species(ia)
  DO l = 0,Ps(isp)%lmax
    DO iprj = 1,Ps(isp)%Nproj_l(l)
      DO m = -l,l
        ibeta = ibeta + 1
        Np_beta = 0
        DO ip = 1,Npoints
          CALL calc_dr_periodic_1pnt( LL, atpos(:,ia), lingrid(:,ip), dr_vec )
          dr = sqrt( dr_vec(1)**2 + dr_vec(2)**2 + dr_vec(3)**2 )
          IF( dr <= Ps(isp)%rcut_NL(l) ) THEN
            Np_beta = Np_beta + 1
            betaNL(ip,ibeta) = hgh_eval_proj_R( Ps(isp), l, iprj, dr ) * &
                               Ylm_real( l, m, dr_vec )
          ENDIF
        ENDDO
        ! Test normalization of projectors
        nrm = sum(betaNL(:,ibeta)**2)*dVol
        WRITE(*,'(1x,A,I5,I8,F18.10)') 'ibeta, Np_beta, integ = ', ibeta, Np_beta, nrm
      ENDDO ! iprj
    ENDDO ! m
  ENDDO ! l
ENDDO
\end{fortrancode}

and {\tt betaNL\_psi} is calculated in {\tt calc\_betaNL\_psi}:
\begin{fortrancode}
! immediate return if no projectors are available
IF( NbetaNL <= 0 ) THEN
  RETURN
ENDIF
betaNL_psi(:,:,:) = 0.d0
DO ia = 1,Natoms
  DO ist = 1,Nstates
    DO ibeta = 1,NbetaNL
      betaNL_psi(ia,ist,ibeta) = ddot( Npoints, betaNL(:,ibeta),1, psi(:,ist),1 ) * dVol
    ENDDO
  ENDDO
ENDDO
\end{fortrancode}


