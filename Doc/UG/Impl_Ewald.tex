\subsection{Ion-Ion interaction}

Via Ewald summation:
Using the code included in PWSCF
\begin{fortrancode}
INTEGER, PARAMETER :: mxr = 50
! setup at and bg
at(:,:) = 0.d0
at(1,1) = LL(1)
at(2,2) = LL(2)
at(3,3) = LL(3)
omega = LL(1)*LL(2)*LL(3)
bg(:,:) = 0.d0
bg(1,1) = TPI/LL(1)
bg(2,2) = TPI/LL(2)
bg(3,3) = TPI/LL(3)
gcutm = maxval( NN )*TPI  !! ???? FIXME
alat = 1.d0
gamma_only = .FALSE.
gstart = 2
charge = 0.d0
DO na = 1, nat
   charge = charge + zv( ityp(na) )
ENDDO
alpha = 2.9d0
100 alpha = alpha - 0.1d0
! choose alpha in order to have convergence in the sum over G
! upperbound is a safe upper bound for the error in the sum over G
IF( alpha <= 0.d0) THEN 
  WRITE(*,*) 'ERROR in calculating Ewald energy:'
  WRITE(*,*) 'optimal alpha not found'
  STOP 
ENDIF 
! beware of unit of gcutm
upperbound = 2.d0*charge**2*sqrt(2.d0*alpha/tpi) * erfc(sqrt(gcutm/4.d0/alpha))  
IF(upperbound > 1.0d-7) GOTO 100
! G-space sum here.
! Determine if this processor contains G=0 and set the constant term
IF(gstart==2) THEN 
  ewaldg = - charge**2 / alpha / 4.0d0
ELSE 
  ewaldg = 0.0d0
ENDIF 
! gamma_only should be .FALSE. for our case
IF(gamma_only) THEN 
  fact = 2.d0
ELSE
  fact = 1.d0
ENDIF 
DO ng = gstart, ngm
  rhon = (0.d0, 0.d0)
  DO nt = 1, ntyp
    rhon = rhon + zv(nt)*CONJG(strf(ng, nt))
  ENDDO
  ewaldg = ewaldg + fact*abs(rhon) **2 * exp( -gg(ng)/alpha/4.d0 )/ gg(ng)
ENDDO
ewaldg = 2.d0 * tpi / omega * ewaldg
!  Here add the other constant term
IF (gstart==2) THEN 
  DO na = 1, nat
    ewaldg = ewaldg - zv(ityp(na))**2 * sqrt(8.d0/tpi*alpha)
  ENDDO
ENDIF 
! R-space sum here (only for the processor that contains G=0)
ewaldr = 0.d0
IF( gstart==2 ) THEN 
  rmax = 4.d0 / sqrt(alpha) / alat
  ! with this choice terms up to ZiZj*erfc(4) are counted (erfc(4)=2x10^-8
  DO na = 1, nat
    DO nb = 1, nat
      dtau(:) = tau(:,na) - tau(:,nb)
      ! generates nearest-neighbors shells
      CALL rgen( dtau, rmax, mxr, at, bg, r, r2, nrm )
      ! and sum to the real space part
      DO nr = 1, nrm
        rr = sqrt (r2 (nr) ) * alat
        ewaldr = ewaldr + zv (ityp (na) ) * zv (ityp (nb) ) * erfc( sqrt (alpha) * rr) / rr
      ENDDO 
    ENDDO 
  ENDDO 
ENDIF 
E_nn = 0.5d0*(ewaldg + ewaldr)
\end{fortrancode}

Listing of {\tt rgen.f90}:
\begin{fortrancode}
SUBROUTINE rgen ( dtau, rmax, mxr, at, bg, r, r2, nrm )
  !
  !   generates neighbours shells (cartesian, in units of lattice parameter)
  !   with length < rmax,and returns them in order of increasing length:
  !      r(:) = i*a1(:) + j*a2(:) + k*a3(:) - dtau(:),   r2 = r^2
  !   where a1, a2, a3 are primitive lattice vectors. Other input variables:
  !     mxr = maximum number of vectors
  !     at  = lattice vectors ( a1=at(:,1), a2=at(:,2), a3=at(:,3) )
  !     bg  = reciprocal lattice vectors ( b1=bg(:,1), b2=bg(:,2), b3=bg(:,3) )
  !   Other output variables:
  !     nrm = the number of vectors with r^2 < rmax^2
  !
  IMPLICIT NONE
  INTEGER, PARAMETER :: DP=8
  INTEGER, INTENT(in) :: mxr
  INTEGER, INTENT(out):: nrm
  REAL(DP), INTENT(in) :: at(3,3), bg(3,3), dtau(3), rmax
  REAL(DP), INTENT(out):: r(3,mxr), r2(mxr)
  !
  !    and here the local variables
  !
  INTEGER, ALLOCATABLE :: irr (:)
  INTEGER ::  nm1, nm2, nm3, i, j, k, ipol, ir, indsw, iswap
  real(DP) :: ds(3), dtau0(3)
  real(DP) :: t (3), tt, swap
  real(DP), EXTERNAL :: dnrm2
  !
  !
  nrm = 0
  IF (rmax==0.d0) RETURN

  ! bring dtau into the unit cell centered on the origin - prevents trouble
  ! if atomic positions are not centered around the origin but displaced
  ! far away (remember that translational invariance allows this!)
  !
  ds(:) = matmul( dtau(:), bg(:,:) )
  ds(:) = ds(:) - anint(ds(:))
  dtau0(:) = matmul( at(:,:), ds(:) )
  !
  ALLOCATE (irr( mxr))
  !
  ! these are estimates of the maximum values of needed integer indices
  !
  nm1 = int (dnrm2 (3, bg (1, 1), 1) * rmax) + 2
  nm2 = int (dnrm2 (3, bg (1, 2), 1) * rmax) + 2
  nm3 = int (dnrm2 (3, bg (1, 3), 1) * rmax) + 2
  !
  DO i = -nm1, nm1
     DO j = -nm2, nm2
        DO k = -nm3, nm3
           tt = 0.d0
           DO ipol = 1, 3
              t (ipol) = i*at (ipol, 1) + j*at (ipol, 2) + k*at (ipol, 3) &
                       - dtau0(ipol)
              tt = tt + t (ipol) * t (ipol)
           ENDDO
           IF (tt<=rmax**2.and.abs (tt) >1.d-10) THEN
              nrm = nrm + 1
              IF (nrm>mxr) then 
                WRITE(*,*) 'ERROR in rgen: too many r-vectors', nrm
                STOP 
              ENDIF 
              DO ipol = 1, 3
                 r (ipol, nrm) = t (ipol)
              ENDDO
              r2 (nrm) = tt
           ENDIF
        ENDDO
     ENDDO
  ENDDO
  !
  !   reorder the vectors in order of increasing magnitude
  !
  !   initialize the index inside sorting routine
  !
  irr (1) = 0
  IF (nrm>1) CALL hpsort (nrm, r2, irr)
  DO ir = 1, nrm - 1
20   indsw = irr (ir)
     IF (indsw/=ir) THEN
        DO ipol = 1, 3
           swap = r (ipol, indsw)
           r (ipol, indsw) = r (ipol, irr (indsw) )
           r (ipol, irr (indsw) ) = swap
        ENDDO
        iswap = irr (ir)
        irr (ir) = irr (indsw)
        irr (indsw) = iswap
        GOTO 20
     ENDIF

  ENDDO
  DEALLOCATE(irr)
  !
  RETURN
END SUBROUTINE rgen
\end{fortrancode}
