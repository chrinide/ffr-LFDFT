\subsection{Pseudopotential}

Module {\tt m\_PsPot}

\begin{fortrancode}
MODULE m_PsPot
  USE m_Ps_HGH, ONLY : Ps_HGH_Params_T
  IMPLICIT NONE 
  CHARACTER(128) :: PsPot_Dir = './HGH/'
  CHARACTER(128), ALLOCATABLE :: PsPot_FilePath(:)
  TYPE(Ps_HGH_Params_T), ALLOCATABLE :: Ps_HGH_Params(:)
  INTEGER :: NbetaNL
  REAL(8), ALLOCATABLE :: betaNL(:,:)
  INTEGER, ALLOCATABLE :: prj2beta(:,:,:,:)
  INTEGER :: NprojTotMax
END MODULE 
\end{fortrancode}

We currently support HGH pseudopotential only.
The HGH pseudopotential parameter is described by an array of type {\tt Ps\_HGH\_Params\_T}
which is defined in {\tt m\_Ps\_HGH}:

\begin{fortrancode}
TYPE Ps_HGH_Params_T
  CHARACTER(5) :: atom_name
  INTEGER :: zval
  REAL(8) :: rlocal
  REAL(8) :: rc(0:3)
  REAL(8) :: c(1:4)
  REAL(8) :: h(0:3, 1:3, 1:3)
  REAL(8) :: k(0:3, 1:3, 1:3)
  INTEGER :: lmax
  INTEGER :: Nproj_l(0:3)  ! number of projectors for each AM
  REAL(8) :: rcut_NL(0:3)
END TYPE
\end{fortrancode}

The array {\tt betaNL} and {\tt prj2beta} are related to nonlocal
pseudopotential calculation.

Except for the array {\tt betaNL}, most variables in module {\tt m\_PsPot}
are initialized by the call to
subroutine {\tt init\_PsPot}.

\begin{fortrancode}
ALLOCATE( PsPot_FilePath(Nspecies) )
ALLOCATE( Ps_HGH_Params(Nspecies) )
DO isp = 1,Nspecies
  PsPot_FilePath(isp) = trim(PsPot_Dir) // trim(SpeciesSymbols(isp)) // '.hgh'
  CALL init_Ps_HGH_Params( Ps_HGH_Params(isp), PsPot_FilePath(isp) )
  AtomicValences(isp) = Ps_HGH_Params(isp)%zval
ENDDO 
\end{fortrancode}

Initialization of array {\tt prj2beta}
\begin{fortrancode}
ALLOCATE( prj2beta(1:3,1:Natoms,0:3,-3:3) )
prj2beta(:,:,:,:) = -1
NbetaNL = 0
DO ia = 1,Natoms
  isp = atm2species(ia)
  DO l = 0,Ps_HGH_Params(isp)%lmax
    DO iprj = 1,Ps_HGH_Params(isp)%Nproj_l(l)
      DO m = -l,l
        NbetaNL = NbetaNL + 1
        prj2beta(iprj,ia,l,m) = NbetaNL
      ENDDO ! m
    ENDDO ! iprj
  ENDDO ! l
ENDDO ! ia
\end{fortrancode}


